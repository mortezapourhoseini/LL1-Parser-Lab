PROGRAM -> DECLARATION_LIST
DECLARATION_LIST -> DECLARATION DECLARATION_LIST | ε
DECLARATION -> VAR_DECLARATION | FUN_DECLARATION
VAR_DECLARATION -> type id ;
FUN_DECLARATION -> type id ( PARAMS ) COMPOUND_STMT
PARAMS -> PARAM_LIST | ε
PARAM_LIST -> PARAM PARAM_LIST'
PARAM_LIST' -> , PARAM PARAM_LIST' | ε
PARAM -> type id
COMPOUND_STMT -> { LOCAL_DECLARATIONS STATEMENT_LIST }
LOCAL_DECLARATIONS -> LOCAL_DECLARATION LOCAL_DECLARATIONS | ε
LOCAL_DECLARATION -> type id ;
STATEMENT_LIST -> STATEMENT STATEMENT_LIST | ε
STATEMENT -> EXPRESSION_STMT | COMPOUND_STMT | RETURN_STMT
EXPRESSION_STMT -> EXPRESSION ; | ;
RETURN_STMT -> return EXPRESSION ;
EXPRESSION -> id = EXPRESSION | SIMPLE_EXPRESSION
SIMPLE_EXPRESSION -> ADDITIVE_EXPRESSION RELOP ADDITIVE_EXPRESSION | ADDITIVE_EXPRESSION
RELOP -> == | != | < | > | <= | >=
ADDITIVE_EXPRESSION -> TERM ADDITIVE_EXPRESSION'
ADDITIVE_EXPRESSION' -> ADDOP TERM ADDITIVE_EXPRESSION' | ε
ADDOP -> + | -
TERM -> FACTOR TERM'
TERM' -> MULOP FACTOR TERM' | ε
MULOP -> * | /
FACTOR -> ( EXPRESSION ) | id | num
